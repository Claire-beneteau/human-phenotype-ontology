PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?x (STR(?lab) AS ?label) ?syn_type WHERE {
	?x rdf:type owl:Class .
	?x ?syn_type ?lab . 
	VALUES ?syn_type { 
		oboInOwl:hasExactSynonym oboInOwl:hasRelatedSynonym oboInOwl:hasBroadSynonym oboInOwl:hasNarrowSynonym
	}
	FILTER(regex(str(?lab), "[. ]$"))
	FILTER(isIRI(?x) && regex(str(?x), "http://purl.obolibrary.org/obo/HP_"))
	FILTER NOT EXISTS {?x owl:deprecated ?y}
}
ORDER BY ?x
